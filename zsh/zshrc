bindkey -e

setopt prompt_subst
setopt interactivecomments
setopt noflowcontrol

export EDITOR="vim"
export MANPAGER="less"
export MANPATH="$MANPATH:.:/home/glabs/local/share/man"
export PATH="$PATH:.:/home/glabs/local/bin"

alias pwd=' pwd'
alias ls=' ls --color=auto'
alias pu='pushd'
alias po='popd'
alias grep='grep --color=auto'
alias valgrind='valgrind --leak-check=full --show-reachable=yes'
alias ocaml='rlwrap ocaml'
alias csi='rlwrap csi'

hash -d epigit='czegan_g@git.epitech.eu:'
hash -d episvn='svn+ssh://kscm@koala-rendus.epitech.eu'
hash -d c='/home/glabs/code'
hash -d w='/home/glabs/code/work'
hash -d v='/home/glabs/code/view'
hash -d a='/home/glabs/code/archive'
hash -d d='/home/glabs/code/done'
hash -d e='/home/glabs/essays'
hash -d p='/home/glabs/papers'
hash -d conf='/home/glabs/.config'

# History
setopt hist_ignore_space
setopt hist_ignore_all_dups
export ISTSIZE=2000
export SAVEHIST=2000
export HISTFILE=~/.history


# Vcs info
autoload -Uz vcs_info
zstyle ':vcs_info:(svn*|git*):*' formats "%c %u [%s|%b]"
zstyle ':vcs_info:*' formats "[%s|%b]"

# Prompt
autoload -U colors && colors
PROMPT="-- %B%{$fg[black]%}%~%b "
RPROMPT='%B%{$fg[black]%}${vcs_info_msg_0_}%b'
PROMPT2="%_: "
precmd () {vcs_info}

# Completion
autoload -U compinit && compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' menu select
zstyle ':completion:*:cd:*' ignore-parents parent pwd

papers() {
    if [[ $# -eq 0 ]]; then
	echo "Usage: papers papers_name" &> /dev/stderr
	return 1
    else
	mkdir -p ~p/unsorted
	scanimage > ~p/unsorted/$1.png
	cd ~p/unsorted
	return 0
    fi
}

compctl -x 'p[1]' -k "(pull push)" - 'p[2]' -g '*(/)' - 'p[3]' -k "(192.168.1.10)" -- mirror
mirror() {
    2=${2:-~/code}
    3=${3:-192.168.1.10}
    case "$1" in
	pull)
	    rsync ${*[4,-1]} -vaz --exclude="*.o" --delete $USER@${3}:${2} ~
	    ;;
	push)
	    rsync ${*[4,-1]} -vaz --exclude="*.o" --delete ${2} $USER@${3}:~
	    ;;
	*)
	    echo "usage: $0 {pull|push} [DEST:~/code] [HOST:192.168.1.10] [RSYNC_OPTION...]"
	    return 1
	    ;;
    esac
}

f() {
    local p=$argv[-1]
    [[ -d $p ]] && { argv[-1]=(); } || p='.'
    find $p ! -type d | sed 's:^./::' | egrep "${@:-.}"
}

wd() {
    local d=${SWAPD:-~}
    export SWAPD=$(pwd)
    [[ $# -eq 0 ]] && cd ${d} || ${1}
}

# From zsh-lovers
rationalise-dot() {
  if [[ $LBUFFER = *.. ]]; then
    LBUFFER+=/..
  else
    LBUFFER+=.
  fi
}

foreground-vi() {
    fg %vi
}

# Binkeys
zle -N rationalise-dot
bindkey . rationalise-dot

zle -N foreground-vi
bindkey '^Z' foreground-vi

bindkey "\e\e[D" backward-word
bindkey "\e\e[C" forward-word
bindkey "^R" history-incremental-pattern-search-backward

# Laziness
[[ $TTY == /dev/tty1 ]] \
  && (( $UID ))         \
  && [[ -z $DISPLAY ]]  \
  && exec startx
